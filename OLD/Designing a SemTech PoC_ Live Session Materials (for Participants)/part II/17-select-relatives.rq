#Use the inference that the transitive and symmetric property relativeOf produces 
#to obtain information about relatives in 2 hops of the top 100 actors.
#Run this query with and without inference

PREFIX imdb: <http://example.org/movieDB/>
PREFIX rank: <http://www.ontotext.com/owlim/RDFRank#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX inst: <http://www.ontotext.com/connectors/lucene/instance#>
PREFIX lucene: <http://www.ontotext.com/connectors/lucene#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT * {
    { SELECT * { 
            ?actor a imdb:Actor ; 
                   foaf:name ?name; 
                   rank:hasRDFRank5 ?r . 
        } ORDER BY DESC(?r) 
        LIMIT 100 
    }
    ?actor skos:exactMatch / imdb:relativeOf / ^skos:exactMatch ?relative . 
    filter(?actor != ?relative )
}   