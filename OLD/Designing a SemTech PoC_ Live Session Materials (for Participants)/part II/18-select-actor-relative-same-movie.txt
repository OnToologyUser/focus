#select among the top 100 actors by RDFRank, who are those playing in the same movie with their relatives, using the inference.

PREFIX imdb: <http://example.org/movieDB/>
PREFIX rank: <http://www.ontotext.com/owlim/RDFRank#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX : <http://example.org/movieDB/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX inst: <http://www.ontotext.com/connectors/lucene/instance#>
PREFIX lucene: <http://www.ontotext.com/connectors/lucene#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT * {
    { SELECT * { 
            ?actor a imdb:Actor ; 
                   foaf:name ?name;   
                   rank:hasRDFRank5 ?r . 
        } ORDER BY DESC(?r) 
        LIMIT 100 }
    ?actor skos:exactMatch / :relativeOf / ^skos:exactMatch ?relative . 
    FILTER(?actor != ?relative ) 
    ?actor :actsIn ?film .
    ?relative :actsIn ?film .
    ?film :title ?film_title .
}    