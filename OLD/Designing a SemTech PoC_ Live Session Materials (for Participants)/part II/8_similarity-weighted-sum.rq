# SH6A: Similar nodes by weighted sum of shared edges

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dul: <http://www.ontologydesignpatterns.org/ont/dul/DUL.owl#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dbc: <http://dbpedia.org/resource/Category:>
PREFIX imdb: <http://example.org/movieDB/>
PREFIX imdb-m: <http://example.org/movieDB/movie/>

SELECT ?similar_node
    (COUNT(DISTINCT ?pair) AS ?shared_pairs)
    (SUM(1/?pair_popularity) AS ?similarity_score)
{
    { SELECT ?node ?p ?o (COUNT(DISTINCT ?different_node) AS ?pair_popularity) {
        BIND( imdb-m:1 AS ?node)
        ?node ?p ?o .
        FILTER(?p NOT IN (  rdf:type, owl:sameAs, imdb:color))
        ?different_node ?p ?o . FILTER(?node != ?different_node)  
    } GROUP BY ?node ?p ?o ORDER BY ?pair_popularity }

    ?similar_node ?p ?o . FILTER(?similar_node != ?node)
    ?similar_node a imdb:Movie .
    BIND(CONCAT(STR(?p), STR(?o)) AS ?pair)  
} GROUP BY ?similar_node ORDER BY DESC(?similarity_score) LIMIT 20