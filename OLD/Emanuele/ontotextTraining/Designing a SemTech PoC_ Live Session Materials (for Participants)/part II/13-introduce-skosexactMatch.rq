#Introduce skos:exactMatch between the resources in our repo and the matched ones in FactForge.net

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX imdb: <http://example.org/movieDB/>
PREFIX rank: <http://www.ontotext.com/owlim/RDFRank#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX : <http://example.org/movieDB/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX inst: <http://www.ontotext.com/connectors/lucene/instance#>
PREFIX lucene: <http://www.ontotext.com/connectors/lucene#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

INSERT {
    GRAPH imdb:dbp_matching {
        ?actor skos:exactMatch ?dbp_actor
    }
} 
WHERE {
    { SELECT * { 
            ?actor a imdb:Actor ; 
                   foaf:name ?name; 
                   rank:hasRDFRank5 ?r . 
        } ORDER BY DESC(?r) 
        LIMIT 1000 }
       BIND(concat('''"''', str(?name) , '''"''') as ?nameQ)
    
    SERVICE <http://factforge.net/repositories/ff-news> {
        ?search a inst:dbp-agents2 ;
            lucene:query ?nameQ ;
            lucene:entities ?dbp_actor ;
            lucene:limit "5" .
        ?dbp_actor lucene:score ?score .
        FILTER(?score > 100)        
        ?dbp_actor rdfs:label ?dbp_label.           
        FILTER(CONTAINS(LCASE(STR(?name)), LCASE(STR(?dbp_label))))    
    }
} 